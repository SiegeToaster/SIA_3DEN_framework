#include "script_component.hpp"

/*
 * Author: Siege
 * Adds all briefing elements (situation, mission, execution, support, signal) to the player's diary with automatic html formatting.  Execute locally.
 *
 * Arguments:
 * 0: Vehicles <ARRAY of OBJECT>
 *
 * Return Value:
 * None
 *
 * Example:
 * [_vehicles] call sia3f_core_fnc_briefing
*/

if (!hasInterface) exitWith {
	LOG_FUNC_END_ERROR("function ran on server");
}; // exit if executed on non client machine

params [
	["_vehicles", [], [[]]]
];

LOG_FUNC_START;
private _situationText = [GET_CONFIG(situationTabText,"")] call FUNC(formatBriefing);
private _missionText = [GET_CONFIG(missionTabText,"")] call FUNC(formatBriefing);
private _executionText = [GET_CONFIG(execTabText,"")] call FUNC(formatBriefing);
private _supportText = [GET_CONFIG(supportTabText,"")] call FUNC(formatBriefing);
private _signalText = [GET_CONFIG(signalTabText,"")] call FUNC(formatBriefing);

TRACE_5("briefing content",_situationText,_missionText,_executionText,_supportText,_signalText);

if !(_signalText isEqualTo "") then { player createDiaryRecord ["Diary", [GET_CONFIG(signalTabName,"Signal"), _signalText]]; };
private "_record";
if !(_supportText isEqualTo "" && _vehicles isEqualTo []) then { _record = player createDiaryRecord ["Diary", [GET_CONFIG(supportTabName,"Support"), _supportText]]; };
if !(_executionText isEqualTo "") then { player createDiaryRecord ["Diary", [GET_CONFIG(execTabName,"Execution"), _executionText]]; };
if !(_missionText isEqualTo "") then { player createDiaryRecord ["Diary", [GET_CONFIG(missionTabName,"Mission"), _missionText]]; };
if !(_situationText isEqualTo "") then { player createDiaryRecord ["Diary", [GET_CONFIG(situationTabName,"Situation"), _situationText]]; };

if (_vehicles isEqualTo []) exitWith { LOG_FUNC_END_ERROR("no support vehicles to add"); };

if !(_supportText isEqualTo "") then { _supportText = _supportText + "<br></br><br></br>"; };
_supportText = _supportText + "<font face='PuristaBold' color='#FFB84C'>Vehicle Assets</font><font face='PuristaLight'> (Automatically Generated by the SIA Framework)</font>:<br></br><br></br>" + ([_vehicles, _record] call FUNC(vehicleInfo));
player setDiaryRecordText [["Diary", _record], [GET_CONFIG(supportTabName,"Support"), _supportText]];

LOG_FUNC_END;
